import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'java'
	id 'idea'
	id "org.flywaydb.flyway" version "6.4.3"
	id 'com.google.cloud.tools.jib' version '2.2.0'
	id 'org.springframework.boot' version '2.3.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

repositories {
	mavenCentral()
	jcenter()
}

group = 'at.lh'
version = '1.0.0'

sourceCompatibility = '11'
targetCompatibility = '11'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

ext {
	set('springCloudVersion', "Hoxton.SR4")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'
	implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
	implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'

	compile "org.flywaydb:flyway-core:6.4.2"
	compile "org.postgresql:postgresql:42.2.5"
	compileOnly 'org.projectlombok:lombok:1.18.12'
	annotationProcessor 'org.projectlombok:lombok:1.18.12'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

flyway {
	url = "jdbc:postgresql://localhost:5432/postgres"
	user = "postgres"
	schemas = ["import_service"]
}


processResources {
	["banner.txt"].each { filesMatching(it) { filter ReplaceTokens, tokens: ["app.version": rootProject.version] } }
}

jib {
	to {
		image = "docker.pkg.github.com/lhaidacher/spring-boot-booking-system/" + project.name
		tags = [project.version]
	}
}
